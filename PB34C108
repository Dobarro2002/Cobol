
      ******************************************************************
      * NOM. DESCR: ESTE PROGRAMA GRABA EN FICH. SECUENCIAL -EMPLEADO- *
      *             DESDE EL FICHERO SECUENCIAL.                       *
      * OBJETIVE:   ESTE PROGRAMA LEE FICHERO DE EMPLEADO Y GRABA NUE- *
      *             VO FICHERO AÃ‘ADIENDO EL DPTO Y EL SALARIO          *
      *             DEPENDIENDO DEL CODIGO DE EMPLEADO.                *
      *             SI EL CODIGO DE EMPLEADO ES <= 29                  *
      *                MOVER 1 AL DPTO          Y 36001 AL SALARIO     *
      *             SINO                                               *
      *             SI EL CODIGO DE EMPLEADO ES >=30 Y <=34            *
      *                MOVER 2 AL DPTO          Y 37007 AL SALARIO     *
      *             SINO                                               *
      *             SI EL CODIGO DE EMPLEADO ES >34      Y <=41        *
      *                MOVER 3 AL DPTO          Y 38500 AL SALARIO     *
      *             FIN-SI                                             *
      * TYPE:       BATCH/COPY                                         *
      * INPUTS:     DATOS DEL SISTEMA (FECHA Y HORA)                   *
      *             DATOS DE SYSIN (TITULO)                            *
      *             FICHERO DE ENTRADA SECUENCIAL -EMPLEADO-:          *
      *                     T9716.T9716XX.ZENT307.EMPLEADO             *
      * OUTPUTS:    FICHERO DE SALIDA SECUENCIAL -EMPLEADO-:           *
      *                     T9716.T9716XX.ZSAL308.EMPLEADO             *
      ******************************************************************

      ******************************************************************
      *                    IDENTIFICATION DIVISION                     *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PB34C108.
       AUTHOR. USUARIO (IRIA).
       DATE-WRITTEN. ENERO, 2021.
       DATE-COMPILED.

      ******************************************************************
      *                    ENVIRONMENT DIVISION                        *
      ******************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.

       SOURCE-COMPUTER. IBM-390.
       OBJECT-COMPUTER. IBM-390.

       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

      ******************************************************************
      *                    INPUT FILE                                  *
      *----------------------------------------------------------------*
      * FICHERO DE EMPLEADO                                            *
      ******************************************************************
           SELECT EMPLEADO
               ASSIGN TO FICHENTR
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE IS SEQUENTIAL
               FILE STATUS IS FS-EMPLEADO.

      ******************************************************************
      *                    OUTPUT FILE                                 *
      *----------------------------------------------------------------*
      * FICHERO DE SALIDA EMPLEADO CON MAS CAMPOS (DPTO Y SALARIO).    *
      ******************************************************************
           SELECT SALIDA
               ASSIGN TO SALJCL
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE IS SEQUENTIAL
               FILE STATUS IS FS-SALIDA.

      ******************************************************************
      *                       DATA DIVISION                            *
      ******************************************************************
       DATA DIVISION.
       FILE SECTION.
       FD  EMPLEADO
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 100 CHARACTERS
           DATA RECORD IS REG-EMPLEADO.
       01  REG-EMPLEADO                   PIC X(100).

       FD  SALIDA
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 100 CHARACTERS
           DATA RECORD IS REG-SALIDA.
       01  REG-SALIDA                     PIC X(100).
      ******************************************************************
      *                   WORKING-STORAGE SECTION                      *
      ******************************************************************
       WORKING-STORAGE SECTION.
       01  WORK-AREA.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO DE EMPLEADO -ENTRADA-    *
      ******************************************************************
           05  WS-IN-REG-EMPLEADO.
       COPY COPYEMPL.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO DE SALIDA.    LONG.:100  *
      ******************************************************************
           05  WS-OU-REG-SALIDA.
               10  WS-OU-CODIGO            PIC 9(5)    VALUE ZEROES.
               10  WS-OU-NOMBRE            PIC X(20)   VALUE SPACES.
               10  WS-OU-CPOSTAL           PIC 9(5)    VALUE ZEROES.
               10  WS-OU-CALLE             PIC X(20)   VALUE SPACES.
               10  WS-OU-DEPAR             PIC 9(4)    VALUE ZEROS.
               10  WS-OU-SALARIO           PIC S9(7)   VALUE ZEROS.
               10  WS-OU-SALARIO-NEW       PIC S9(7)V9(2) COMP-3
                                   VALUE ZEROS.
               10 FILLER                   PIC X(03)   VALUE 'FIN'.
               10 FILLER                   PIC X(31)   VALUE SPACES.

      ******************************************************************
      *                 FILE STATUS                                    *
      ******************************************************************
           05  FS-EMPLEADO                  PIC 99      VALUE ZEROS.
           05  FS-SALIDA                    PIC 99      VALUE ZEROS.

      ******************************************************************
      *                 FLAGS AND SWITCHES                             *
      ******************************************************************
           05  END-OF-SWITCH               PIC X       VALUE 'N'.
               88  END-OF-FILE                         VALUE 'Y'.
               88  NOT-END-OF-FILE                     VALUE 'N'.

           05  END-FILE-SWITCH             PIC X       VALUE 'N'.
               88  EMPTY-FILE                          VALUE 'Y'.

           05  ERRORES-SWITCH              PIC X       VALUE 'N'.
               88 ERRORES                              VALUE 'Y'.

      ******************************************************************
      *                 COUNTERS AND ACCUMULATORS                      *
      ******************************************************************
           05  CTR-LEIDOS                  PIC S9(9) COMP-3
                                   VALUE ZEROS.
           05  CTR-GRABADOS                PIC S9(9) COMP-3
                                   VALUE ZEROS.
           05  CTR-LEIDOS-E                PIC  ZZZ.ZZZ.ZZZ+
                                   VALUE ZEROS.
           05  CTR-GRABADOS-E              PIC  ZZZ.ZZZ.ZZZ+
                                   VALUE ZEROS.
           05  AUX-SALARIO-E               PIC  Z.ZZZ.ZZZ,ZZ+
                                   VALUE ZEROS.

      ******************************************************************
      *                 CONSTANTS AND LITERALS                         *
      ******************************************************************
           05  CTE-PROGRAMA                PIC X(8)    VALUE 'PB34C108'.
           05  CTE-1                       PIC 9       VALUE 1.
           05  CTE-2                       PIC 9       VALUE 2.
           05  CTE-3                       PIC 9       VALUE 3.
           05  CTE-29                      PIC 99      VALUE 29.
           05  CTE-30                      PIC 99      VALUE 30.
           05  CTE-34                      PIC 99      VALUE 34.
           05  CTE-35                      PIC 99      VALUE 35.
           05  CTE-41                      PIC 99      VALUE 41.
           05  CTE-1001                    PIC 9(4)    VALUE 1001.
           05  CTE-36001                   PIC 9(5)    VALUE 36001.
           05  CTE-37007                   PIC 9(5)    VALUE 37007.
           05  CTE-38500                   PIC 9(5)    VALUE 38500.
           05  CTE-GUIONES                 PIC X(70)   VALUE ALL '='.
           05  CTE-NOMBRE-LARGO            PIC X(70)
                                             VALUE 'ESTE ES UN EJEMPLO D
      -    'E CONTINUIDAD DE LINEA'.
      ******************************************************************
      *                 SCRATCH AREA                                   *
      ******************************************************************
           05  AUX-SYSIN.
               10  AUX-TITULO              PIC X(40)    VALUE SPACES.
               10  AUX-RESTO               PIC X(40)    VALUE SPACES.
           05  AUX-FECHA.
               10  AUX-ANYO                PIC 9(4)     VALUE ZERO.
               10  AUX-MES                 PIC 9(2)     VALUE ZERO.
               10  AUX-DIA                 PIC 9(2)     VALUE ZERO.

           05  AUX-HOR.
               10 AUX-HORA                 PIC 9(2)     VALUE ZERO.
               10 AUX-MINUTO               PIC 9(2)     VALUE ZERO.
               10 AUX-SEGUNDO              PIC 9(2)     VALUE ZERO.
               10 AUX-MILI                 PIC 9(2)     VALUE ZERO.

      ******************************************************************
      *                       PROCEDURE DIVISION                       *
      ******************************************************************
       PROCEDURE DIVISION.

      ******************************************************************
      *                       1000-MAINLINE                            *
      *----------------------------------------------------------------*
      * ESTE PARRAFO CONTROLA LA ESTRUCTURA DEL PROCESO DEL PROGRAMA   *
      ******************************************************************
       1000-MAINLINE.
           PERFORM 2000-HOUSEKEEPING
              THRU 2000-HOUSEKEEPING-EXIT.

           PERFORM 3000-PROCESS
              THRU 3000-PROCESS-EXIT
             UNTIL END-OF-FILE
                OR ERRORES.

           PERFORM 8000-WRAP-UP
              THRU 8000-WRAP-UP-EXIT.

           STOP RUN.

      ******************************************************************
      *                       2000-HOUSEKEEPING                        *
      *----------------------------------------------------------------*
      *  ABRE FICHEROS,   LEE FICHERO Y CONTROLA ERRORES SI ESTA VACIO *
      ******************************************************************
       2000-HOUSEKEEPING.
           ACCEPT AUX-FECHA  FROM DATE YYYYMMDD.
           ACCEPT AUX-HOR    FROM TIME.
           ACCEPT AUX-TITULO FROM SYSIN.

           DISPLAY  'COMIENZA EL PROGRAMA: ' CTE-PROGRAMA
               ' FECHA: ' AUX-ANYO '-' AUX-MES '-' AUX-DIA
               ' HORA: '      AUX-HORA ':' AUX-MINUTO ':' AUX-SEGUNDO
               ':' AUX-MILI.
           DISPLAY AUX-TITULO
           DISPLAY CTE-GUIONES.
           DISPLAY CTE-NOMBRE-LARGO.
           DISPLAY CTE-GUIONES.

           SET NOT-END-OF-FILE TO TRUE.
      *    MOVE 'N'  TO END-OF-SWITCH.

           OPEN INPUT EMPLEADO
               OUTPUT SALIDA.

           IF  FS-EMPLEADO NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR EMPLEADO ' FS-EMPLEADO

               SET ERRORES TO TRUE
           END-IF.

           IF  FS-SALIDA NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR SALIDA ' FS-SALIDA

               SET ERRORES TO TRUE
           END-IF.

           PERFORM 9000-LEER
              THRU 9000-LEER-EXIT.

           IF END-OF-FILE
              SET EMPTY-FILE TO TRUE
           END-IF.

       2000-HOUSEKEEPING-EXIT.
           EXIT.

      ******************************************************************
      *                       3000-PROCESS                             *
      *----------------------------------------------------------------*
      *  ESTE PARRAFO ASIGNA DPTO Y SALARIO. ESCRIBE FICHERO SALIDA    *
      *  CONTROLA ERROR AL ESCRIBIR Y VUELVE A LEER OTRO REG.EMPLEADO. *
      ******************************************************************
       3000-PROCESS.
           PERFORM 3100-ASIGNAR
              THRU 3100-ASIGNAR-EXIT.

           WRITE REG-SALIDA FROM WS-OU-REG-SALIDA.

           IF  FS-SALIDA EQUAL TO ZEROS
               ADD CTE-1 TO CTR-GRABADOS

           ELSE
               DISPLAY 'ERROR AL GRABAR SALIDA ' FS-SALIDA

               SET ERRORES TO TRUE
           END-IF.

           PERFORM 9000-LEER
              THRU 9000-LEER-EXIT.

       3000-PROCESS-EXIT.
           EXIT.

      ******************************************************************
      *                       3100-ASIGNAR                             *
      *----------------------------------------------------------------*
      *  ESTE PARRAFO ASGINA DPTO Y SALARIO EN EL REGISTRO DE SALIDA   *
      ******************************************************************
       3100-ASIGNAR.
           INITIALIZE WS-OU-REG-SALIDA.

           MOVE WS-IN-REG-EMPLEADO(1:50) TO WS-OU-REG-SALIDA(1:50).

           EVALUATE WS-IN-CODIGO
               WHEN CTE-1 THRU CTE-29
                    MOVE CTE-1     TO WS-OU-DEPAR
                    MOVE CTE-36001 TO WS-OU-SALARIO
                                      WS-OU-SALARIO-NEW

               WHEN CTE-30 THRU CTE-34
                    MOVE CTE-2     TO WS-OU-DEPAR
                    MOVE CTE-37007 TO WS-OU-SALARIO
                                      WS-OU-SALARIO-NEW

               WHEN CTE-35 THRU CTE-41
                    MOVE CTE-3     TO WS-OU-DEPAR
                    MOVE CTE-38500 TO WS-OU-SALARIO
                                      WS-OU-SALARIO-NEW
               WHEN OTHER
                    MOVE ZERO      TO WS-OU-DEPAR
                    MOVE ZERO      TO WS-OU-SALARIO
                                      WS-OU-SALARIO-NEW
           END-EVALUATE.

           MOVE WS-OU-SALARIO      TO AUX-SALARIO-E.

           DISPLAY 'SALARIOS: ' WS-OU-SALARIO ' COMP-3 '
                                WS-OU-SALARIO-NEW
                    ' EDITADO: ' AUX-SALARIO-E.

        3100-ASIGNAR-EXIT.
             EXIT.

      ******************************************************************
      *                       8000-WRAP-UP                             *
      *----------------------------------------------------------------*
      * ESTE PROCESO CONTROLA VACIO/ERRORES, CIERRA FICHERO Y          *
      * MUESTRA CONTADORES                                             *
      ******************************************************************
       8000-WRAP-UP.
           IF  EMPTY-FILE THEN
               DISPLAY 'FICHERO EMPLEADO VACIO.'

           ELSE
               IF ERRORES THEN
           DISPLAY '///////////////////////////'
           DISPLAY '// EJECUCION NO CORRECTA //'
           DISPLAY '///////////////////////////'

                  MOVE CTE-1001 TO RETURN-CODE

              ELSE
                  CLOSE EMPLEADO
                        SALIDA
              END-IF
           END-IF.
           MOVE CTR-LEIDOS           TO CTR-LEIDOS-E.
           MOVE CTR-GRABADOS         TO CTR-GRABADOS-E.

           DISPLAY CTE-GUIONES.
           DISPLAY 'LEIDOS EMPLEADO: ' CTR-LEIDOS-E.
           DISPLAY 'GRABADOS SALIDA: ' CTR-GRABADOS-E.
           DISPLAY 'FIN DE PROGRAMA: ' CTE-PROGRAMA.

       8000-WRAP-UP-EXIT.
           EXIT.

      ******************************************************************
      *                       9000-LEER                                *
      ******************************************************************
       9000-LEER.
           READ EMPLEADO INTO WS-IN-REG-EMPLEADO
               AT END
                   SET END-OF-FILE TO TRUE
               NOT AT END
                   ADD CTE-1 TO CTR-LEIDOS
           END-READ.

       9000-LEER-EXIT.
           EXIT.
