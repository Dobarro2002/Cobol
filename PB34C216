
      ******************************************************************
      * NOM. DESCR: ESTE PROGRAMA HACE CRUCE DE FICHEROS 1:1           *
      * OBJETIVE:   ESTE PROGRAMA LEE FICHERO MAESTRO ALUMNO Y         *
      *             SI EXISTE IGUALDAD ENTRE AMBOS CODIGOS DE ALUMNO   *
      *             SE INCREMENTA EL BRUTO CON EL DE SUBIDAS           *
      *             Y RECALCULAR EL NETO CON EL NUEVO BRUTO.           *
      *             SI NO EXISTE IGUALDAD Y LA CLAVE DEL MAESTRO ES    *
      *             MENOR QUE LA CLAVE DE SUBIDAS, SE GRABA TAL COMO   *
      *             EST√Å EN EL MAESTRO.                                *
      *             SI FUERA MAYOR LA CLAVE DEL MAESTRO A LA DE SUBIDA,*
      *             SE GRABA EN EL DE ERRORES.                         *
      *                                                                *
      * TYPE:       BATCH/COPY                                         *
      * INPUTS:     DATOS DEL SISTEMA (FECHA Y HORA)                   *
      *             MAESTRO DE ALUMNOS               (PS)              *
      *                     T9716.T9716XX.ZENTRADA.ALUMNO              *
      *             FICHERO DE SUBIDAS DE SALARIO    (PS)              *
      *                     T9716.T9716XX.ZENT216.SUBIALUM             *
      * OUTPUTS:    FICHERO NUEVO-ALUMNOS            (PS)              *
      *                     T9716.T9716XX.ZSAL216.MAEACT               *
      *             FICHERO ERRORES                  (PS)              *
      *                     T9716.T9716XX.ZSAL216.ERRORES              *
      ******************************************************************

      ******************************************************************
      *                    IDENTIFICATION DIVISION                     *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PB34C216.
       AUTHOR. USUARIO (IRIA).
       DATE-WRITTEN. ENERO, 2021.
       DATE-COMPILED.

      ******************************************************************
      *                    ENVIRONMENT DIVISION                        *
      ******************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.

       SOURCE-COMPUTER. IBM-390.
       OBJECT-COMPUTER. IBM-390.

       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

      ******************************************************************
      *                    INPUT FILE                                  *
      *----------------------------------------------------------------*
      * FICHEROS DE ENTRADA DE SUBIDA                                  *
      ******************************************************************
           SELECT SUBIDA
               ASSIGN TO SUBIDA
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE  IS SEQUENTIAL
               FILE STATUS  IS FS-SUBIDA.

      ******************************************************************
      *                    INPUT FILE                                  *
      *----------------------------------------------------------------*
      * FICHEROS DE ENTRADA DE MAESTRO DE EMPLEADO                     *
      ******************************************************************
           SELECT MAESTRO
               ASSIGN TO MAESTRO
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE IS SEQUENTIAL
               FILE STATUS IS FS-MAESTRO.

      ******************************************************************
      *                    OUTPUT FILE                                 *
      *----------------------------------------------------------------*
      * FICHERO DE NUEVO MAESTRO ACTUALIZADO                           *
      ******************************************************************
           SELECT MAEACT
               ASSIGN TO MAEACT
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE IS SEQUENTIAL
               FILE STATUS IS FS-MAEACT.

      ******************************************************************
      *                    OUTPUT FILE                                 *
      *----------------------------------------------------------------*
      * FICHERO DE ERRORES                                             *
      ******************************************************************
           SELECT FICERROR
               ASSIGN TO FICERROR
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE IS SEQUENTIAL
               FILE STATUS IS FS-FICERROR.

      ******************************************************************
      *                       DATA DIVISION                            *
      ******************************************************************
       DATA DIVISION.
       FILE SECTION.
       FD  SUBIDA
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 70 CHARACTERS
           DATA RECORD IS REG-SUBIDA.
       01  REG-SUBIDA                     PIC X(70).

       FD  MAESTRO
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 70 CHARACTERS
           DATA RECORD IS REG-MAESTRO.
       01  REG-MAESTRO                    PIC X(70).

       FD  MAEACT
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 70 CHARACTERS
           DATA RECORD IS REG-MAEACT.
       01  REG-MAEACT                     PIC X(70).

       FD  FICERROR
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 70 CHARACTERS
           DATA RECORD IS REG-FICERROR.
       01  REG-FICERROR                   PIC X(70).

      ******************************************************************
      *                   WORKING-STORAGE SECTION                      *
      ******************************************************************
       WORKING-STORAGE SECTION.
       01  WORK-AREA.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO DE SUBIDA ALUMNO-ENTRADA-*
      ******************************************************************
           05  WS-IN-REG-SUBIDA.
               10  WS-IN-CLAVE.
                   15  WS-IN-CURSO         PIC X(3)    VALUE SPACES.
                   15  WS-IN-NOMBRE        PIC X(17)   VALUE SPACES.

               10  FILLER                  PIC X(36)   VALUE SPACES.
               10  WS-IN-BRUTO             PIC S9(9) COMP-3
                                    VALUE ZEROS.
               10  FILLER                  PIC X(09)   VALUE SPACES.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO ALUMNO    -ENTRADA-      *
      ******************************************************************
           05  WS-IN-REG-ALUMNO.
               10  WS-IN-CLAVE.
                   15  WS-IN-CURSO         PIC X(3)    VALUE SPACES.
                   15  WS-IN-NOMBRE        PIC X(17)   VALUE SPACES.

               10  WS-IN-TITULACION        PIC X(14)   VALUE SPACES.
               10  WS-IN-ESTUDIOS          PIC X(14)   VALUE SPACES.
               10  WS-IN-INGLES            PIC X(06)   VALUE SPACES.
               10  WS-IN-PROV              PIC 9(2)    VALUE ZEROS.
               10  WS-IN-BRUTO             PIC S9(9) COMP-3
                                    VALUE ZEROS.
               10  WS-IN-IRPF              PIC 9(02)   VALUE ZEROS.
               10  WS-IN-NETO              PIC S9(7)V99 COMP-3
                                    VALUE ZEROS.
               10  FILLER                  PIC X(2)    VALUE SPACES.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO DEL MAESTRO ACTUALIZADO  *
      ******************************************************************
           05  WS-OU-REG-ALUMNO.
               10  WS-0U-CLAVE.
                   15  WS-0U-CURSO         PIC X(3)    VALUE SPACES.
                   15  WS-OU-NOMBRE        PIC X(17)   VALUE SPACES.

               10  WS-OU-TITULACION        PIC X(14)   VALUE SPACES.
               10  WS-OU-ESTUDIOS          PIC X(14)   VALUE SPACES.
               10  WS-OU-INGLES            PIC X(06)   VALUE SPACES.
               10  WS-OU-PROV              PIC 9(2)    VALUE ZEROS.
               10  WS-OU-BRUTO             PIC S9(9) COMP-3
                                    VALUE ZEROS.
               10  WS-OU-IRPF              PIC 9(02)   VALUE ZEROS.
               10  WS-OU-NETO              PIC S9(7)V99 COMP-3
                                    VALUE ZEROS.
               10  FILLER                  PIC X(2)    VALUE SPACES.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO DE ERRORES               *
      ******************************************************************
           05  WS-OU-REG-FICERROR.
               10  WS-OU-CLAVE.
                   15  WS-OU-CURSO         PIC X(3)    VALUE SPACES.
                   15  WS-OU-NOMBRE        PIC X(17)   VALUE SPACES.

               10  FILLER                  PIC X(36)   VALUE SPACES.
               10  WS-OU-BRUTO             PIC S9(9) COMP-3
                                    VALUE ZEROS.
               10  FILLER                  PIC X(09)   VALUE SPACES.

      ******************************************************************
      *                 FILE STATUS                                    *
      ******************************************************************
           05  FS-SUBIDA                    PIC 99      VALUE ZEROS.
           05  FS-MAESTRO                   PIC 99      VALUE ZEROS.
           05  FS-MAEACT                    PIC 99      VALUE ZEROS.
           05  FS-FICERROR                  PIC 99      VALUE ZEROS.

      ******************************************************************
      *                 FLAGS AND SWITCHES                             *
      ******************************************************************
           05  END-OF-SWITCH-MAESTRO       PIC X       VALUE 'N'.
               88  END-OF-FILE-MAESTRO                 VALUE 'Y'.
               88  NOT-END-OF-FILE-MAESTRO             VALUE 'N'.

           05  END-OF-SWITCH-SUBIDA        PIC X       VALUE 'N'.
               88  END-OF-FILE-SUBIDA                  VALUE 'Y'.
               88  NOT-END-OF-FILE-SUBIDA              VALUE 'N'.

           05  END-FILE-SWITCH-MAESTRO     PIC X       VALUE 'N'.
               88  EMPTY-FILE-MAESTRO                  VALUE 'Y'.

           05  END-FILE-SWITCH-SUBIDA      PIC X       VALUE 'N'.
               88  EMPTY-FILE-SUBIDA                   VALUE 'Y'.

           05  ERRORES-SWITCH              PIC X       VALUE 'N'.
               88 ERRORES                              VALUE 'Y'.

      ******************************************************************
      *                 COUNTERS AND ACCUMULATORS                      *
      ******************************************************************
           05  CTR-LEIDOS-SUBIDA           PIC S9(9) COMP-3
                                   VALUE ZEROS.
           05  CTR-LEIDOS-MAESTRO          PIC S9(9) COMP-3
                                   VALUE ZEROS.
           05  CTR-MAEACT                  PIC S9(9) COMP-3
                                   VALUE ZEROS.
           05  CTR-ERRORES                 PIC S9(9) COMP-3
                                   VALUE ZEROS.
           05  CTR-ACTUALIZADOS            PIC S9(9) COMP-3
                                   VALUE ZEROS.

      ******************************************************************
      *                 CONSTANTS AND LITERALS                         *
      ******************************************************************
           05  CTE-1                       PIC 9       VALUE 1.
           05  CTE-10                      PIC 99      VALUE 10.
           05  CTE-1001                    PIC 9(4)    VALUE 1001.
           05  CTE-GUIONES                 PIC X(70)   VALUE ALL '='.
           05  CTE-PROGRAMA                PIC X(8)    VALUE 'PB34C216'.

      ******************************************************************
      *                 SCRATCH AREA                                   *
      ******************************************************************
           05  AUX-TITULO                  PIC X(60)    VALUE SPACES.
           05  AUX-CLAVE-MAESTRO           PIC X(20)    VALUE SPACES.
           05  AUX-CLAVE-SUBIDA            PIC X(20)    VALUE SPACES.
           05  AUX-RESU-MULTIPLY-NETO      PIC 9(18)    VALUE ZEROES.
           05  AUX-RESU-DIVIDE-NETO        PIC 9(18)    VALUE ZEROES.
           05  AUX-RESU-SUBTRACT-NETO      PIC 9(18)    VALUE ZEROES.
           05  AUX-FECHA.
               10  AUX-ANYO                PIC 9(4)     VALUE ZERO.
               10  AUX-MES                 PIC 9(2)     VALUE ZERO.
               10  AUX-DIA                 PIC 9(2)     VALUE ZERO.

           05  AUX-HOR.
               10 AUX-HORA                 PIC 9(2)     VALUE ZERO.
               10 AUX-MINUTO               PIC 9(2)     VALUE ZERO.
               10 AUX-SEGUNDO              PIC 9(2)     VALUE ZERO.
               10 AUX-MILI                 PIC 9(2)     VALUE ZERO.

      ******************************************************************
      *                       PROCEDURE DIVISION                       *
      ******************************************************************
       PROCEDURE DIVISION.

      ******************************************************************
      *                       1000-MAINLINE                            *
      *----------------------------------------------------------------*
      * ESTE PARRAFO CONTROLA LA ESTRUCTURA DEL PROCESO DEL PROGRAMA   *
      ******************************************************************
       1000-MAINLINE.
           PERFORM 2000-HOUSEKEEPING
              THRU 2000-HOUSEKEEPING-EXIT.

           PERFORM 3000-PROCESS
              THRU 3000-PROCESS-EXIT
             UNTIL (END-OF-FILE-MAESTRO
               AND END-OF-FILE-SUBIDA)
                OR ERRORES.

           PERFORM 8000-WRAP-UP
              THRU 8000-WRAP-UP-EXIT.

           STOP RUN.

      ******************************************************************
      *                       2000-HOUSEKEEPING                        *
      *----------------------------------------------------------------*
      * ABRE LOS FICHEROS Y LEE SUBIDA Y MAESTRO, CONTROLA SI VACIO    *
      ******************************************************************
       2000-HOUSEKEEPING.
           ACCEPT AUX-FECHA  FROM DATE YYYYMMDD.
           ACCEPT AUX-HOR    FROM TIME.
           ACCEPT AUX-TITULO FROM SYSIN.

           DISPLAY  'COMIENZA EL PROGRAMA: ' CTE-PROGRAMA
               ' FECHA: ' AUX-ANYO '-' AUX-MES '-' AUX-DIA
               ' HORA: '      AUX-HORA ':' AUX-MINUTO ':' AUX-SEGUNDO
               ':' AUX-MILI.
           DISPLAY AUX-TITULO
           DISPLAY CTE-GUIONES.

           OPEN INPUT SUBIDA
                      MAESTRO
               OUTPUT MAEACT
                      FICERROR.

           IF  FS-SUBIDA NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR SUBIDA ' FS-SUBIDA

               SET ERRORES TO TRUE
           END-IF.

           IF  FS-MAESTRO NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR MAESTRO ' FS-MAESTRO

               SET ERRORES TO TRUE
           END-IF.

           IF  FS-MAEACT NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR MAEACT ' FS-MAEACT

               SET ERRORES TO TRUE
           END-IF.

           IF  FS-FICERROR NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR FICERROR ' FS-FICERROR

               SET ERRORES TO TRUE
           END-IF.

           PERFORM 9000-LEER-SUBIDA
              THRU 9000-LEER-SUBIDA-EXIT.

           PERFORM 9100-LEER-MAESTRO
              THRU 9100-LEER-MAESTRO-EXIT.

           IF END-OF-FILE-MAESTRO
              SET EMPTY-FILE-MAESTRO TO TRUE
              SET ERRORES TO TRUE
           END-IF.

           IF END-OF-FILE-SUBIDA
              SET EMPTY-FILE-SUBIDA TO TRUE
              SET ERRORES TO TRUE
           END-IF.

       2000-HOUSEKEEPING-EXIT.
           EXIT.

      ******************************************************************
      *                       3000-PROCESS                             *
      *----------------------------------------------------------------*
      *  ESTE PARRAFO COMPARA CLAVES (CODIGO) PARA DETERMINAR ACCION.  *
      ******************************************************************
       3000-PROCESS.
           EVALUATE TRUE
               WHEN AUX-CLAVE-MAESTRO EQUAL TO AUX-CLAVE-SUBIDA
                   PERFORM 3100-INCREMENTAR
                      THRU 3100-INCREMENTAR-EXIT

               WHEN AUX-CLAVE-MAESTRO LESS THAN AUX-CLAVE-SUBIDA
                   PERFORM 3200-MANTENER
                      THRU 3200-MANTENER-EXIT

               WHEN AUX-CLAVE-MAESTRO GREATER THAN AUX-CLAVE-SUBIDA
                   PERFORM 3300-ERROR
                      THRU 3300-ERROR-EXIT

           END-EVALUATE.

       3000-PROCESS-EXIT.
           EXIT.

      ******************************************************************
      *                       3100-INCREMENTAR                         *
      *----------------------------------------------------------------*
      * AQUI SE INCREMENTA EL BRUTO CON LA SUBIDA. SE ESCRIBE EN EL    *
      * DE MAEACT CON EL CAMBIO Y SE LEE DE AMBOS.                     *
      ******************************************************************
       3100-INCREMENTAR.
           MOVE WS-IN-REG-ALUMNO TO WS-OU-REG-ALUMNO.

           ADD WS-IN-BRUTO OF WS-IN-REG-SUBIDA TO
               WS-OU-BRUTO IN WS-OU-REG-ALUMNO.

           MULTIPLY WS-OU-BRUTO OF WS-OU-REG-ALUMNO BY
                    WS-OU-IRPF  OF WS-OU-REG-ALUMNO GIVING
                    AUX-RESU-MULTIPLY-NETO.

             DIVIDE AUX-RESU-MULTIPLY-NETO BY 100
                    GIVING AUX-RESU-DIVIDE-NETO.

           SUBTRACT WS-OU-BRUTO OF WS-OU-REG-ALUMNO FROM
                    AUX-RESU-DIVIDE-NETO GIVING AUX-RESU-SUBTRACT-NETO.

           MOVE AUX-RESU-SUBTRACT-NETO TO
                WS-OU-NETO IN WS-OU-REG-ALUMNO.

               WRITE REG-MAEACT FROM WS-OU-REG-ALUMNO.

               IF  FS-MAEACT EQUAL TO ZEROS
                   ADD CTE-1 TO CTR-MAEACT
                                CTR-ACTUALIZADOS

               ELSE
                   DISPLAY 'ERROR AL GRABAR MAEACT. FS= ' FS-MAEACT

                   SET ERRORES TO TRUE
               END-IF.

               PERFORM 9000-LEER-SUBIDA
                  THRU 9000-LEER-SUBIDA-EXIT.

               PERFORM 9100-LEER-MAESTRO
                  THRU 9100-LEER-MAESTRO-EXIT.
        3100-INCREMENTAR-EXIT.
             EXIT.

      ******************************************************************
      *                       3200-MANTENER                            *
      *----------------------------------------------------------------*
      * GRABA EL REGISTRO EN MAEACT CON EL MISMO BRUTO Y LEE MAESTRO   *
      ******************************************************************
       3200-MANTENER.
           MOVE WS-IN-REG-ALUMNO TO WS-OU-REG-ALUMNO.

           WRITE REG-MAEACT FROM WS-OU-REG-ALUMNO.

           IF  FS-MAEACT EQUAL TO ZEROS
               ADD CTE-1 TO CTR-MAEACT

           ELSE
               DISPLAY 'ERROR AL GRABAR MAEACT ' FS-MAEACT

               SET ERRORES TO TRUE
            END-IF.

            PERFORM 9100-LEER-MAESTRO
               THRU 9100-LEER-MAESTRO-EXIT.

        3200-MANTENER-EXIT.
             EXIT.

      ******************************************************************
      *                       3300-ERRORESR                            *
      *----------------------------------------------------------------*
      * GRABA EL REGISTRO EN ERRORES CON DATOS SUBIDA Y LEE SUBIDA     *
      ******************************************************************
       3300-ERROR.
           MOVE WS-IN-REG-SUBIDA TO WS-OU-REG-FICERROR.

           WRITE REG-FICERROR FROM WS-OU-REG-FICERROR.

           IF  FS-FICERROR EQUAL TO ZEROS
               ADD CTE-1 TO CTR-ERRORES

           ELSE
               DISPLAY 'ERROR AL GRABAR FIC-ERRORES. FS= ' FS-FICERROR

               SET ERRORES TO TRUE
            END-IF.

            PERFORM 9000-LEER-SUBIDA
               THRU 9000-LEER-SUBIDA-EXIT.

        3300-ERROR-EXIT.
             EXIT.

      ******************************************************************
      *                       8000-WRAP-UP                             *
      *----------------------------------------------------------------*
      * ESTE PROCESO CONTROLA VACIO/ERRORES, CIERRA FICHERO Y          *
      * MUESTRA CONTADORES.                                            *
      ******************************************************************
       8000-WRAP-UP.
           EVALUATE TRUE
               WHEN EMPTY-FILE-MAESTRO AND EMPTY-FILE-SUBIDA
                   DISPLAY 'FICHERO ALUMNO Y FICHERO SUBIDA VACIOS.'

                   MOVE CTE-1001 TO RETURN-CODE

               WHEN EMPTY-FILE-MAESTRO
                   DISPLAY 'FICHERO ALUMNO VACIO'

                   MOVE CTE-1001 TO RETURN-CODE

               WHEN EMPTY-FILE-SUBIDA
                   DISPLAY 'FICHERO SUBIDA VACIO.'

                   MOVE CTE-1001 TO RETURN-CODE

              WHEN ERRORES
                  DISPLAY '//=======================//'
                  DISPLAY '//-----------------------//'
                  DISPLAY '// EJECUCION NO CORRECTA //'
                  DISPLAY '//-----------------------//'
                  DISPLAY '//=======================//'

                  MOVE CTE-1001 TO RETURN-CODE

              WHEN OTHER
                  CLOSE SUBIDA
                        MAESTRO
                        MAEACT
                        FICERROR
           END-EVALUATE.

           DISPLAY CTE-GUIONES.
           DISPLAY 'LEIDOS SUBIDA     : ' CTR-LEIDOS-SUBIDA.
           DISPLAY 'LEIDOS MAESTRO    : ' CTR-LEIDOS-MAESTRO.
           DISPLAY 'GRABADOS MAEACT   : ' CTR-MAEACT.
           DISPLAY 'ACTUALIZADOS      : ' CTR-ACTUALIZADOS.
           DISPLAY 'GRABADOS ERRORES  : ' CTR-ERRORES
           DISPLAY 'FIN DE PROGRAMA.    ' CTE-PROGRAMA.
           DISPLAY CTE-GUIONES.

       8000-WRAP-UP-EXIT.
           EXIT.

      ******************************************************************
      *                       9000-LEER-SUBIDA                         *
      *----------------------------------------------------------------*
      * LECTURA DE FICHERO SUBIDA. CONTROL DE EOF Y HIGH-VALUES.       *
      ******************************************************************
       9000-LEER-SUBIDA.
           READ SUBIDA INTO WS-IN-REG-SUBIDA.

           EVALUATE FS-SUBIDA
               WHEN ZEROS
                   ADD CTE-1 TO CTR-LEIDOS-SUBIDA

                   MOVE WS-IN-CLAVE OF WS-IN-REG-SUBIDA
                     TO AUX-CLAVE-SUBIDA

               WHEN CTE-10
                   SET END-OF-FILE-SUBIDA TO TRUE

                   MOVE HIGH-VALUES TO AUX-CLAVE-SUBIDA

               WHEN OTHER
                   DISPLAY 'ERROR AL LEER SUBIDA ' FS-SUBIDA

                   SET ERRORES TO TRUE
           END-EVALUATE.

       9000-LEER-SUBIDA-EXIT.
           EXIT.

      ******************************************************************
      *                       9100-LEER-MAESTRO                        *
      *----------------------------------------------------------------*
      * LECTURA DE FICHERO MAESTRO. CONTROL DE EOF Y HIGH-VALUES.      *
      ******************************************************************
       9100-LEER-MAESTRO.
           READ MAESTRO INTO WS-IN-REG-ALUMNO.

           EVALUATE FS-MAESTRO
               WHEN ZEROS
                   ADD CTE-1 TO CTR-LEIDOS-MAESTRO

                   MOVE WS-IN-CLAVE OF WS-IN-REG-ALUMNO
                     TO AUX-CLAVE-MAESTRO

               WHEN CTE-10
                   SET END-OF-FILE-MAESTRO TO TRUE

                   MOVE HIGH-VALUES TO AUX-CLAVE-MAESTRO

               WHEN OTHER
                   DISPLAY 'ERROR AL LEER MAESTRO ' FS-MAESTRO

                   SET ERRORES TO TRUE
           END-EVALUATE.

       9100-LEER-MAESTRO-EXIT.
           EXIT.
