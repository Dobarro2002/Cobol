
      ******************************************************************
      * NOM. DESCR: ESTE PROGRAMA PROCESA EL FICH.SECUENCIAL -CLIENTE- *
      * OBJETIVE:   ESTE PROGRAMA LEE EL FICHERO DE CLIENTE Y POR CADA *
      *             REGISTRO LEIDO, SACARA UN REGISTRO EN EL FICHERO   *
      *             DE SALIDA CON EL FORMATO DESEADO DONDE:            *
      *         1 - AGRUPAREMOS  NOMBRE, AP1 Y AP2 EN UN SOLO CAMPO    *
      *             LLAMADO WS-OU-NOMBRE-COMPLETO                      *
      *             -STRING-                                           *
      *         2 - DESAGRUPAREMOS LA DIRECCION EN DOS CAMPOS EN EL    *
      *             FICHERO DE SALIDA QUE SON WS-OU-CALLE Y WS-OU-RESTO*
      *             DETECTANDO LA SEPARACION POR LA COMA.              *
      *             -UNSTRING-                                         *
      *         3 - DESAGRUPANDO LA FECHA EN TRES CAMPOS -ANO, MES, DIA*
      *             DETECTANDO LA SEPARACION POR EL GUION.             *
      *             -UNSTRING-                                         *
      * TYPE:       BATCH REPORT                                       *
      * INPUTS:     DATOS DEL SISTEMA (FECHA Y HORA)                   *
      *             DATOS DE SYSIN (TITULO DEL LISTADO)                *
      *             FICHERO DE ENTRADA SECUENCIAL -CLIENTE-:           *
      *                   T9716.X.CLIENTE.CARGA                        *
      * OUTPUTS:    FICHERO DE SALIDA SECUENCIAL   -CLIESAL-           *
      *                   T9716.T9716XX.ZSALC04.CLIENTE                *
      ******************************************************************


      ******************************************************************
      *                    IDENTIFICATION DIVISION                     *
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PB34C04.
       AUTHOR. USUARIO (IRIA).
       DATE-WRITTEN. ENERO, 2021.
       DATE-COMPILED.

      ******************************************************************
      *                    ENVIRONMENT DIVISION                        *
      ******************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.

       SOURCE-COMPUTER. IBM-390.
       OBJECT-COMPUTER. IBM-390.

       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

      ******************************************************************
      *                    INPUT FILE                                  *
      *----------------------------------------------------------------*
      * FICHERO DE CLIENTE                                             *
      ******************************************************************
           SELECT CLIENTE
               ASSIGN TO FICHENTR
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE IS SEQUENTIAL
               FILE STATUS IS FS-CLIENTE.

      ******************************************************************
      *                    OUTPUT FILE                                 *
      *----------------------------------------------------------------*
      * FICHERO DE SALIDA CLIENTE CON MAS CAMPOS (DPTO Y SALARIO).     *
      ******************************************************************
           SELECT SALIDA
               ASSIGN TO SALJCL
               ORGANIZATION IS SEQUENTIAL
               ACCESS MODE IS SEQUENTIAL
               FILE STATUS IS FS-SALIDA.

      ******************************************************************
      *                       DATA DIVISION                            *
      ******************************************************************
       DATA DIVISION.
       FILE SECTION.
       FD  CLIENTE
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 120 CHARACTERS
           DATA RECORD IS REG-CLIENTE.
       01  REG-CLIENTE                    PIC X(120).

       FD  SALIDA
           LABEL RECORDS ARE STANDARD
           RECORDING MODE IS F
           BLOCK CONTAINS 0 RECORDS
           RECORD CONTAINS 120 CHARACTERS
           DATA RECORD IS REG-SALIDA.
       01  REG-SALIDA                     PIC X(120).
      ******************************************************************
      *                   WORKING-STORAGE SECTION                      *
      ******************************************************************
       WORKING-STORAGE SECTION.
       01  WORK-AREA.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO DE CLIENTE    LONG.: 120 *
      ******************************************************************
           05  WS-IN-REG-CLIENTE.
               10  WS-IN-CODIGO            PIC 9(04)   VALUE ZEROES.
               10  WS-IN-NOMBRE            PIC X(20)   VALUE SPACES.
               10  WS-IN-APELLIDO1         PIC X(20)   VALUE SPACES.
               10  WS-IN-APELLIDO2         PIC X(20)   VALUE SPACES.
               10  WS-IN-DIRECCION         PIC X(30)   VALUE SPACES.
               10  WS-IN-CODPOSTAL         PIC 9(05)   VALUE ZEROES.
               10  WS-IN-FECHA             PIC X(10)   VALUE SPACES.
               10  FILLER                  PIC X(11)   VALUE SPACES.

      ******************************************************************
      *                   RECORD LAYOUT                                *
      *----------------------------------------------------------------*
      *  DESCRIPCION DEL REGISTRO DEL FICHERO DE SALIDA     LONG.: 120 *
      ******************************************************************
           05  WS-OU-REG-SALIDA.
               10  WS-OU-CODIGO            PIC 9(04)   VALUE ZEROES.
               10  WS-OU-NOMBRE-COMPLETO   PIC X(60)   VALUE SPACES.
               10  WS-OU-CALLE             PIC X(20)   VALUE SPACES.
               10  WS-OU-RESTO-DIRECCION   PIC X(10)   VALUE SPACES.
               10  WS-OU-CODPOSTAL         PIC 9(05)   VALUE ZEROES.
               10  WS-OU-ANYO              PIC 9(04)   VALUE ZEROES.
               10  WS-OU-MES               PIC 9(02)   VALUE ZEROES.
               10  WS-OU-DIA               PIC 9(02)   VALUE ZEROES.
               10  FILLER                  PIC X(13)   VALUE SPACES.

      ******************************************************************
      *                 FILE STATUS                                    *
      ******************************************************************
           05  FS-CLIENTE                   PIC 99      VALUE ZEROS.
           05  FS-SALIDA                    PIC 99      VALUE ZEROS.

      ******************************************************************
      *                 FLAGS AND SWITCHES                             *
      ******************************************************************
           05  END-OF-SWITCH               PIC X       VALUE 'N'.
               88  END-OF-FILE                         VALUE 'Y'.
               88  NOT-END-OF-FILE                     VALUE 'N'.

           05  END-FILE-SWITCH             PIC X       VALUE 'N'.
               88  EMPTY-FILE                          VALUE 'Y'.

           05  ERRORES-SWITCH              PIC X       VALUE 'N'.
               88 ERRORES                              VALUE 'Y'.

      ******************************************************************
      *                 COUNTERS AND ACCUMULATORS                      *
      ******************************************************************
           05  CTR-LEIDOS                  PIC S9(9) COMP-3
                                   VALUE ZEROS.
           05  CTR-GRABADOS                PIC S9(9) COMP-3
                                   VALUE ZEROS.

      ******************************************************************
      *                 CONSTANTS AND LITERALS                         *
      ******************************************************************
           05  CTE-PROGRAMA                PIC X(8)    VALUE 'PB34C04'.
           05  CTE-1                       PIC 9       VALUE 1.
           05  CTE-1001                    PIC 9(4)    VALUE 1001.
           05  CTE-GUIONES                 PIC X(70)   VALUE ALL '='.

      ******************************************************************
      *                 SCRATCH AREA                                   *
      ******************************************************************
           05  AUX-SYSIN.
               10  AUX-TITULO              PIC X(40)    VALUE SPACES.
               10  AUX-RESTO               PIC X(40)    VALUE SPACES.
           05  AUX-FECHA.
               10  AUX-ANYO                PIC 9(4)     VALUE ZERO.
               10  AUX-MES                 PIC 9(2)     VALUE ZERO.
               10  AUX-DIA                 PIC 9(2)     VALUE ZERO.

           05  AUX-HOR.
               10 AUX-HORA                 PIC 9(2)     VALUE ZERO.
               10 AUX-MINUTO               PIC 9(2)     VALUE ZERO.
               10 AUX-SEGUNDO              PIC 9(2)     VALUE ZERO.
               10 AUX-MILI                 PIC 9(2)     VALUE ZERO.

      ******************************************************************
      *                       PROCEDURE DIVISION                       *
      ******************************************************************
       PROCEDURE DIVISION.

      ******************************************************************
      *                       1000-MAINLINE                            *
      *----------------------------------------------------------------*
      * ESTE PARRAFO CONTROLA LA ESTRUCTURA DEL PROCESO DEL PROGRAMA   *
      ******************************************************************
       1000-MAINLINE.
           PERFORM 2000-HOUSEKEEPING
              THRU 2000-HOUSEKEEPING-EXIT.

           PERFORM 3000-PROCESS
              THRU 3000-PROCESS-EXIT
             UNTIL END-OF-FILE
                OR ERRORES.

           PERFORM 8000-WRAP-UP
              THRU 8000-WRAP-UP-EXIT.

           STOP RUN.

      ******************************************************************
      *                       2000-HOUSEKEEPING                        *
      *----------------------------------------------------------------*
      *  ABRE FICHEROS,   LEE FICHERO Y CONTROLA ERRORES SI ESTA VACIO *
      ******************************************************************
       2000-HOUSEKEEPING.
           ACCEPT AUX-FECHA  FROM DATE YYYYMMDD.
           ACCEPT AUX-HOR    FROM TIME.
           ACCEPT AUX-TITULO FROM SYSIN.

           DISPLAY  'COMIENZA EL PROGRAMA: ' CTE-PROGRAMA
               ' FECHA: ' AUX-ANYO '-' AUX-MES '-' AUX-DIA
               ' HORA: '      AUX-HORA ':' AUX-MINUTO ':' AUX-SEGUNDO
               ':' AUX-MILI.
           DISPLAY AUX-TITULO
           DISPLAY CTE-GUIONES.

           SET NOT-END-OF-FILE TO TRUE.
      *    MOVE 'N'  TO END-OF-SWITCH.

           OPEN INPUT CLIENTE
               OUTPUT SALIDA.

           IF  FS-CLIENTE NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR CLIENTE ' FS-CLIENTE

               SET ERRORES TO TRUE
           END-IF.

           IF  FS-SALIDA NOT EQUAL TO ZEROS
               DISPLAY 'ERROR AL ABRIR SALIDA ' FS-SALIDA

               SET ERRORES TO TRUE
           END-IF.

           PERFORM 9000-LEER
              THRU 9000-LEER-EXIT.

           IF END-OF-FILE
              SET EMPTY-FILE TO TRUE
           END-IF.

       2000-HOUSEKEEPING-EXIT.
           EXIT.

      ******************************************************************
      *                       3000-PROCESS                             *
      *----------------------------------------------------------------*
      *  ESTE PARRAFO ASIGNA DPTO Y SALARIO. ESCRIBE FICHERO SALIDA    *
      *  CONTROLA ERROR AL ESCRIBIR Y VUELVE A LEER OTRO REG.CLIENTE. *
      ******************************************************************
       3000-PROCESS.
           INITIALIZE WS-OU-REG-SALIDA.
               STRING WS-IN-NOMBRE DELIMITED BY SPACE
                      WS-IN-APELLIDO1 DELIMITED BY SPACE
                      WS-IN-APELLIDO2 DELIMITED BY SPACE
                      INTO WS-OU-NOMBRE-COMPLETO
               END-STRING.

               UNSTRING WS-IN-DIRECCION DELIMITED BY ","
                   INTO WS-OU-CALLE, WS-OU-RESTO-DIRECCION
               END-UNSTRING.

               UNSTRING WS-IN-FECHA DELIMITED BY "-"
                   INTO WS-OU-ANYO, WS-OU-MES, WS-OU-DIA
               END-UNSTRING.

           WRITE REG-SALIDA FROM WS-OU-REG-SALIDA.

           IF  FS-SALIDA EQUAL TO ZEROS
               ADD CTE-1 TO CTR-GRABADOS

           ELSE
               DISPLAY 'ERROR AL GRABAR SALIDA ' FS-SALIDA

               SET ERRORES TO TRUE
           END-IF.

           PERFORM 9000-LEER
              THRU 9000-LEER-EXIT.

       3000-PROCESS-EXIT.
           EXIT.

      ******************************************************************
      *                       8000-WRAP-UP                             *
      *----------------------------------------------------------------*
      * ESTE PROCESO CONTROLA VACIO/ERRORES, CIERRA FICHERO Y          *
      * MUESTRA CONTADORES                                             *
      ******************************************************************
       8000-WRAP-UP.
           IF  EMPTY-FILE THEN
               DISPLAY 'FICHERO CLIENTE VACIO.'

           ELSE
               IF ERRORES THEN
           DISPLAY '///////////////////////////'
           DISPLAY '// EJECUCION NO CORRECTA //'
           DISPLAY '///////////////////////////'

                  MOVE CTE-1001 TO RETURN-CODE

              ELSE
                  CLOSE CLIENTE
                        SALIDA
              END-IF
           END-IF.

           DISPLAY CTE-GUIONES.
           DISPLAY 'LEIDOS CLIENTE: ' CTR-LEIDOS.
           DISPLAY 'GRABADOS SALIDA: ' CTR-GRABADOS.
           DISPLAY 'FIN DE PROGRAMA: ' CTE-PROGRAMA.

       8000-WRAP-UP-EXIT.
           EXIT.

      ******************************************************************
      *                       9000-LEER                                *
      ******************************************************************
       9000-LEER.
           READ CLIENTE INTO WS-IN-REG-CLIENTE
               AT END
                   SET END-OF-FILE TO TRUE
               NOT AT END
                   ADD CTE-1 TO CTR-LEIDOS
           END-READ.

       9000-LEER-EXIT.
           EXIT.
